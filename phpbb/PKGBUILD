developer=http://phpbb.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.2.2
pkgrel=1
pkgdesc="Free, open source bulletin board software"
arch=('any')
license=("GPL2")
source=("https://www.phpbb.com/files/release/phpBB-${pkgver}.zip")
releasepage=("https://www.phpbb.com/downloads/")
depends=(
    'php'
)
options=('!strip')
sha512sums=('32a003e5165458e65c76286bf2893cebeb1dc81a03dd05cce90e971270d6ddd29cf7b7b0a844c9efe25239d0485d278a18f2173f57294b5f2009466e591ec357')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/*.pl ${pkgdir}/usr/share/${pkgname}/bin/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/*.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/usr/share/${pkgname}/phpbb
    cp -a $srcdir/phpBB3/* ${pkgdir}/usr/share/${pkgname}/phpbb/

# Data
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
