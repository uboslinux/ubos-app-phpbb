developer=http://phpbb.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorrel=3.3
pkgver=${_majorrel}.4
pkgrel=1
pkgdesc="Free, open source bulletin board software"
arch=('any')
license=("GPL2")
source=("https://download.phpbb.com/pub/release/${_majorrel}/${pkgver}/phpBB-${pkgver}.zip")
releasepage=("https://www.phpbb.com/downloads/")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('a51ed237e25e2e53f0f841f0b1af57dae4067de62767845e8f7d2dbca7852c87dafa42221970c7d43d0fea2e8578b9e566e44eb7fbaef9f4888479c4df88a70b')

prepare() {
    cd ${srcdir}/phpBB3
    patch -p0 < ${startdir}/null-container.patch
}

package() {
# Manifest
    install -D -m644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code
    install -D -m755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/phpbb
    cp -a ${srcdir}/phpBB3/* ${pkgdir}/ubos/share/${pkgname}/phpbb/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
