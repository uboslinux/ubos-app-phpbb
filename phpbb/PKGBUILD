developer=http://phpbb.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorrel=3.3
pkgver=${_majorrel}.3
pkgrel=2
pkgdesc="Free, open source bulletin board software"
arch=('any')
license=("GPL2")
source=("https://download.phpbb.com/pub/release/${_majorrel}/${pkgver}/phpBB-${pkgver}.zip")
releasepage=("https://www.phpbb.com/downloads/")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('34c8c0e222bd5279e67b1c8cf55ddb6e0718b5f020f2af82c5d12cf6443a4d9748eb0a6998314fe0e4c488a37c41994086525e16b2bafc581142c9c0b5768f1d')

package() {
# Manifest
    install -D -m644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code
    install -D -m755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/phpbb
    cp -a ${srcdir}/phpBB3/* ${pkgdir}/ubos/share/${pkgname}/phpbb/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
