developer=http://phpbb.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.2.3
pkgrel=1
pkgdesc="Free, open source bulletin board software"
arch=('any')
license=("GPL2")
source=("https://www.phpbb.com/files/release/phpBB-${pkgver}.zip")
releasepage=("https://www.phpbb.com/downloads/")
depends=(
    'php'
)
options=('!strip')
sha512sums=('c55918e703986eb07430528e2dcaebc1d1db18b03dfa63c431b2c9b453ee09a13a3ae4deb960514e8f514d19dd66f9b0b1fd6424d25d299f437797c600ccc50b')

package() {
# Manifest
    install -D -m644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code
    install -D -m755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/phpbb
    cp -a ${srcdir}/phpBB3/* ${pkgdir}/ubos/share/${pkgname}/phpbb/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
