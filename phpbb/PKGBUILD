developer=http://phpbb.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.2.4
pkgrel=1
pkgdesc="Free, open source bulletin board software"
arch=('any')
license=("GPL2")
source=("https://www.phpbb.com/files/release/phpBB-${pkgver}.zip")
releasepage=("https://www.phpbb.com/downloads/")
depends=(
    'php'
)
options=('!strip')
sha512sums=('ed961648a82ec360610a65b8a1805e165426decd78fe93320c9aefca939c2420542988551b9e829a28cb40fb465d8c95ff940505fb93347d3fb14a8368bc0b87')

package() {
# Manifest
    install -D -m644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code
    install -D -m755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/phpbb
    cp -a ${srcdir}/phpBB3/* ${pkgdir}/ubos/share/${pkgname}/phpbb/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
