developer=http://phpbb.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorrel=3.3
pkgver=${_majorrel}.2
pkgrel=1
pkgdesc="Free, open source bulletin board software"
arch=('any')
license=("GPL2")
source=("https://download.phpbb.com/pub/release/${_majorrel}/${pkgver}/phpBB-${pkgver}.zip")
releasepage=("https://www.phpbb.com/downloads/")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('8a93740aefa857b52736ddf8c9e8264eb6ee1d9a461befb0814db44b8695147fbfd9bfa331dada06047cc5a2b36a82d94b761fc010e0e96f6d4426106945dcbd')

package() {
# Manifest
    install -D -m644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code
    install -D -m755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/phpbb
    cp -a ${srcdir}/phpBB3/* ${pkgdir}/ubos/share/${pkgname}/phpbb/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
